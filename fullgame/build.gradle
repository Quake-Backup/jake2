plugins {
    // http://stehno.com/gradle-natives/
    // This plugin automatically detects native libs in compile/runtime jars
    // and extracts them into `natives` folder.
    id 'com.stehno.natives' version '0.3.1'
}

apply plugin: 'application'

mainClassName = 'jake2.fullgame.Jake2'

// to run with `gradle run`
run.systemProperty "java.library.path", "${project.buildDir}/natives"

startScripts {
    // this gets into the start scripts
    defaultJvmOpts = [ "-Djava.library.path=.."]
}

dependencies {
    compile project(':client')
    compile project(':server')

    runtime project(':game')
}

distributions {
    main {
        contents {
            from "build/natives"
        }
    }
}

installDist.dependsOn('includeNatives')
distZip.dependsOn('includeNatives')
distTar.dependsOn('includeNatives')
run.dependsOn('includeNatives')
