plugins {
    // http://stehno.com/gradle-natives/
    // This plugin automatically detects native libs in compile/runtime jars
    // and extracts them into `natives` folder.
    id 'com.stehno.natives' version '0.3.1'
    id 'application'
}

group 'org.bytonic'
version '1.0.0'

ext.lwjgl_version='2.9.3'
sourceCompatibility = 1.8

// to run with `gradle run`
run.systemProperty "java.library.path", "${project.buildDir}/natives"

startScripts {
    // this gets into the start scripts
    defaultJvmOpts = [ "-Djava.library.path=.."]
}

mainClassName = 'jake2.Jake2'

sourceSets {
    main.java.srcDirs = ['src']
    main.resources.srcDirs = ['resources']
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.lwjgl.lwjgl:lwjgl:${lwjgl_version}"
    runtime "org.lwjgl.lwjgl:lwjgl-platform:${lwjgl_version}"
    compile "org.lwjgl.lwjgl:lwjgl_util:${lwjgl_version}"
    compile 'javazoom:jlayer:1.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

distributions {
    main {
        contents {
            from "build/natives"
        }
    }
}

installDist.dependsOn('includeNatives')
distZip.dependsOn('includeNatives')
distTar.dependsOn('includeNatives')
run.dependsOn('includeNatives')
